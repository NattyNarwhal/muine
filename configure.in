AC_INIT(src/Muine.cs)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(muine, 0.4.0)

AM_MAINTAINER_MODE

AC_PROG_INTLTOOL([0.21])

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_LIBTOOL

GNOME_DEBUG_CHECK
GNOME_COMPILE_WARNINGS(error)
AM_PATH_GLIB_2_0

GSTREAMER_REQUIRED=0.6.3
MONO_REQUIRED=0.30.1
GTKSHARP_REQUIRED=0.16

PKG_CHECK_MODULES(MUINE,
		  gstreamer-0.6 >= $GSTREAMER_REQUIRED \
		  gstreamer-gconf-0.6 >= $GSTREAMER_REQUIRED \
		  gconf-2.0 \
		  gdk-pixbuf-2.0 \
		  gnome-vfs-2.0 \
		  gtk+-2.0)
AC_SUBST(MUINE_CFLAGS)
AC_SUBST(MUINE_LIBS)

PKG_CHECK_MODULES(GTKSHARP,
		  mono >= $MONO_REQUIRED \
		  gtk-sharp >= $GTKSHARP_REQUIRED)

dnl Handle GConf
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
AM_GCONF_SOURCE_2

dnl Check for GDBM
AC_CHECK_HEADER(gdbm.h,,AC_MSG_ERROR(you need gdbm))
GDBM_LIBS="-lgdbm"
AC_SUBST(GDBM_LIBS)

dnl Check for Ogg Vorbis
MM_PATH_OGG(, AC_MSG_ERROR(You need Ogg))
AC_SUBST(OGG_CFLAGS)
AC_SUBST(OGG_LIBS)

MM_PATH_VORBIS(, AC_MSG_ERROR(You need Vorbis))
AC_SUBST(VORBIS_CFLAGS)
AC_SUBST(VORBIS_LIBS)
AC_SUBST(VORBISFILE_CFLAGS)
AC_SUBST(VORBISFILE_LIBS)

dnl Check for libid3tag
PKG_CHECK_MODULES(ID3TAG, id3tag >= 0.15, HAVE_ID3TAG="yes", HAVE_ID3TAG="no")
if test "x$HAVE_ID3TAG" = "xno"; then
	AC_CHECK_HEADER(id3tag.h, ID3TAG_LIBS="-lid3tag -lz", AC_MSG_ERROR(You need libid3tag))
fi
AC_SUBST(ID3TAG_LIBS)

dnl Check for FLAC
AC_CHECK_HEADER(FLAC/all.h, FLAC_LIBS="-lFLAC", AC_MSG_ERROR(You need FLAC))
AC_SUBST(FLAC_LIBS)

dnl Check for Mono
AC_PATH_PROG(MCS, mcs)
AC_PATH_PROG(MONO, mono)

MCS_FLAGS=
MONO_FLAGS=
if test $USE_MAINTAINER_MODE = yes; then
	MCS_FLAGS="-g"
	MONO_FLAGS="--debug"
fi
AC_SUBST(MCS_FLAGS)
AC_SUBST(MONO_FLAGS)

dnl Intl
ALL_LINGUAS="cs de es fr hr nl sr sr@Latn sv"
GETTEXT_PACKAGE=muine
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package])
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
Makefile
muine.spec
data/Makefile
data/glade/Makefile
data/images/Makefile
libmuine/Makefile
libmuine/id3-vfs/Makefile
libmuine/egg/Makefile
src/Makefile
src/muine
m4/Makefile
po/Makefile.in
po/Makefile
])
